buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.0"
group = "org.valkyrienskies.addon" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "vs-world"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_39"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

ext {
    lombokVersion = "1.18.10"
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    deobfCompile("org.valkyrienskies:Valkyrien-Skies:4ceb3621") {
        transitive = false
    }
    shade "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72"
}

// region SHADOW

shadowJar {
    classifier = null // No -all classifier
    configurations = [project.configurations.shade]
    relocate 'kotlin', 'org.valkyrienskies.addons.world.deps.kotlin'
    relocate 'jetbrains', 'org.valkyrienskies.addons.world.deps.jetbrains'
    relocate 'intellij', 'org.valkyrienskies.addons.world.deps.intellij'
    mergeServiceFiles() // https://github.com/johnrengelman/shadow/issues/304
}

reobf {
    shadowJar{}
}

// https://forums.spongepowered.org/t/mcp-reobfuscation-doesnt-work/21779
tasks.build.dependsOn reobfShadowJar

reobf.jar {
    mappingType = "SEARGE"
    classpath = sourceSets.main.compileClasspath
}

// endregion

jar {
    classifier = "unshaded"
    manifest.attributes(
            "FMLAT": "vs_world_at.cfg"
    )
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ValkyrienSkies/vs-world")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "org.valkyrienskies.addon"
            artifactId = "vs-world"
            version = project.version

            from components.java
            artifact shadowJar
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
