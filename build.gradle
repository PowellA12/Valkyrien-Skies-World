buildscript {
    repositories {
        jcenter()
        maven {
            name = "MinecraftForge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle.forge'

ext {
    //general things
    javaVersion = "1.8"
    vsWorldVersion = "0.0.1"

    //minecraft things
    mcpmappingsVersion = "stable_39"
    minecraftforgeVersion = "14.23.5.2847"
    minecraftVersion = "1.12.2"

    //dependency things
    forgelinVersion = "1.8.4"
    vsVersion = "4ceb3621"
}

version = "$vsWorldVersion-$minecraftVersion"
group = "org.valkyrienskies.addon"
archivesBaseName = "vs-world"

compileJava {
    sourceCompatibility = targetCompatibility = "$javaVersion"
}
compileKotlin.kotlinOptions.jvmTarget = "$javaVersion"
compileTestKotlin.kotlinOptions.jvmTarget = "$javaVersion"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

minecraft {
    version = "$minecraftVersion-$minecraftforgeVersion"
    runDir = "run"
    mappings = "$mcpmappingsVersion"
    makeObfSourceJar = false
}

repositories {
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
    }
    maven {
        name = "Shadowfacts"
		url "http://maven.shadowfacts.net/"
	}
    mavenCentral()
}

dependencies {
    // The mod depends on Valkyrien Skies
    deobfCompile("org.valkyrienskies:Valkyrien-Skies:$vsVersion") {
        transitive = false
    }
    compile "net.shadowfacts:Forgelin:$forgelinVersion"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

reobf.jar {
    mappingType = "SEARGE"
    classpath = sourceSets.main.compileClasspath
}

jar {
    archiveName "vs-world-v$vsWorldVersion-$minecraftVersion" + ".jar"
    
    manifest.attributes("FMLAT": "vs_world_at.cfg")
}

publishing {
    repositories {
        if (project.hasProperty("daporkchopMavenUsername") && project.hasProperty("daporkchopMavenPassword")) {
            maven {
                credentials {
                    username = "$daporkchopMavenUsername"
                    password = "$daporkchopMavenPassword"
                }

                name = "DaPorkchop_"
                url = "https://maven.daporkchop.net/snapshot/"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "org.valkyrienskies.addon"
            artifactId = "vs-world"
            version = project.version

            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

